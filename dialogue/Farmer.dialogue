~ start
if GameStateHolder.FarmerAngry:
	=> FarmerAngry
elif (not GameStateHolder.getPlayerSpokeWithFarmer()):
	Farmer: What's with all the new faces? First those adventurers, then the Orc, now you.
	- What do you mean, "Now me"?
		Farmer: Look at you. You look like trouble and nothing else. 
		- Hurtful
			=> Drake
		- Fair
			=> Drake
	- The Orc actually sent me to you. 
		Farmer: What did she do that for? 
		- She's worried about you. 
			Farmer: Is she now. Never thought her the caring type. She sure left in a hurry after she stabbed that drake. 
			- Not the caring type?
			- She told me she saved you.
			Farmer: Dammit, I probably wasn't supposed to say that. Actually... 
			- Go on. 
				Farmer: She didn't kill it. 
				- What do you mean?
					Farmer: It basically killed itself, and she is just claiming that she was the one to cause his death. Don't ask me, adventurers are weird like that. 
					Farmer: Just don't tell her. She swore bad things would happen if I spilled the tea.
					Farmer: Can you leave me alone now?
					set GameStateHolder.KnowsOrcLied = true
					- Sure 
						=> DialogueEnd
					- What happened to it?
						Farmer: Flew into the windmill, impaled itself on the wood. She just stabbed it after so it looks like she did it.
						Farmer: If she finds out I said that I'm dead meat. Go. I already said too much. 
						set GameStateHolder.KnowsOrcLied = true
						=> DialogueEnd
			- Why weren't you supposed to say that?
				Farmer: So, truth is, she didn't kill it. It flew into the windmill on its own and she stabbed it after. Claimed it as her kill, and then threatened me to never tell anyone. 
				set GameStateHolder.KnowsOrcLied = true
		- She told me a story. I thought you might have some details to add. 
	- What adventurers? [if GameStateHolder.timeOfDay != 2]
		Farmer: The trio of bumbling idiots that wanted to solve the drake idiot
		Farmer: Said they were from the guild, but when I asked if they could prove that they didn't show me anyhting
		- What did they look like?
			Farmer: One large man in armor so heavy not three of me could lift it. 
			Farmer: A sly looking guy that I wouldn't trust with taking care of a pile of scraps
			Farmer: A twitchy girl that was way too warmly dressed for the season.
			- So an incredibly suspicious crew. Understood. 
				Farmer: Yeah. You don't have it out for them do you?
				Farmer: They might have been a bit sketchy, but I would trust them to be much stronger than you
				- No interest in them
					set GameStateHolder.KnowsAboutAdventurers = true
					=> Drake
				- They could have some fun stories to tell. 
					Farmer: They said they'd be back later. So stick around, and you might just get to ask them. 
					Farmer: And if you're in the mood you can be an eyewitness if they get any funny ideas. 
					- Will do. 
					- I'd rather be somewhere else. 
					Farmer: Suit yourself. 
					set GameStateHolder.KnowsAboutAdventurers = true
					=> Drake 
			- Any clue where they are now?
				Farmer: Gods know where they are. Said they'd back later to try again. I hope not. 
				Farmer: But if you wanna stick around, they might actually keep their word. 
				- Will do. 
				- I'd rather not. 
				Farmer: Suit yourself
				set GameStateHolder.KnowsAboutAdventurers = true
				=> Drake
		- Where are they now?
			Farmer: Gods know where they are. Said they'd back later to try again. I hope not. 
			Farmer: But if you wanna stick around, they might actually keep their word. 
			- Will do. 
			- I'd rather not. 
			Farmer: Suit yourself. 
			set GameStateHolder.KnowsAboutAdventurers = true
			=> Drake
else if ((not GameStateHolder.SpokeToFarmerDayOneEvening) and GameStateHolder.KnowsAboutAdventurers and GameStateHolder.timeOfDay == 2):
	=> SecondStart
else:
	=> Clueless


~ Drake
Farmer: Either way, you wanted something. Let's get it over with. 
- I wanted to ask about that Drake.
	Farmer: Okay? What about it?
	- Just came here to look at it. 
		Farmer: Alright, when you've finished your gawking, be on your way. 
		=> DialogueEnd
	- The Orc told me a story. Wanted to know if she told the truth. 
		=> Story
- Actually, I'll just leave you to it. 
	Farmer: Thank you kindly. Pray the rest of the day will be just as peaceful. 
	=> DialogueEnd 

~ Story
Farmer: Whatever she told you is the truth. 
- Are you hiding something?
	Farmer: No. What would I be hiding?
	- Then why so dodgy?
		Farmer: Because... well... Ah dammit. 
		Farmer: Is this like a test? She sent you to try and find out if I would betray her?
		- No, but why would she?
			Farmer: So you're not in league with her?
			- Not at all. 
			- I'm just interested in the truth 
			Farmer: Alright so. Thing is, she didn't kill the drake. It kinda just flew into the windmill, and she took the opportunity
			Farmer: It just pretty much died on it's own. I was the only one who saw it.
			Farmer: So she went and told me that if I told a Soul, I'd be in trouble.
			set GameStateHolder.KnowsOrcLied = true
			- I get it now. 
				Farmer: Alright, good. Can you leave now? I don't want her to see us together. 
				- Do you know where she is now?
					Farmer: Probably still in the tavern, making herself out to be a Hero.
					=> DialogueEnd
				- Sure.
				=> DialogueEnd 
			- Don't worry, she won't find out it was you.
				Farmer: I'd hope so. Otherwise I'm gonna be coming after you. 
				- Slow down with the threat there, buddy. 
				- I'd like to see you try. 
				Farmer: S-sorry. Nerves are getting to me. 
				- Do you know where she is now?
					Farmer: Probably still in the tavern, making herself out to be a Hero.
					=> DialogueEnd
				- Anything else you wanted to add?
					Farmer: Nothing comes to mind. Oh! Maybe if you could chase the kids away? 
					Farmer: I don't think being around a Dead Drake's body is a good influence. 
					- Sure thing. 
						=> DialogueEnd
					- Do it yourself
						=> DialogueEnd  
		- yes, and you failed. 
			set GameStateHolder.FarmerAngry = true
			Farmer: Dammit. Leave me alone! I didn't want this! I was just in the wrong place at the wrong time!
			- Yeah, and I hope you know better now. 
				Farmer: I do. Leave, go tell on me. 
				- Actually, I'm just trying to figure out the truth. 
					Farmer: Wait really? You just gave me a heartattack for what, for funsies?
					- Sure I did. Was quite funny, no?
						Farmer: You have a funny idea of funny.
						- Don't be like that. 
							Farmer: I believe I will. I have nothing more to say to you. Goodbye.
							=> DialogueEnd 
						- So what actually happened?
							Farmer: Oh, now you want to know. Too bad. 
							Farmer: Goodbye. 
							=> DialogueEnd
					- Sorry about that. Could you tell me what actually happened?
						Farmer: Alright so. Thing is, she didn't kill the drake. It kinda just flew into the windmill, and she took the opportunity
						Farmer: It just pretty much died on it's own. I was the only one who saw it.
						set GameStateHolder.KnowsOrcLied = true
						- Thank you. I'll be on my way. 
						=> DialogueEnd
				- I will. You better prepare. 
					=> DialogueEnd
			- Okay, so what actually happened?
				Farmer: Shouldn't you know already?
				- I sure do. I want you to remind me.
				- I mean, maybe. I'd still like to hear your side. 
				Farmer: I mean. Really? Isn't this just a waste of both of our time?
				- Just tell me what happened already. 
					Farmer: You aren't actually in league with her, are you?
					- Not at all. 
						Farmer: Great waste of my time. 
						Farmer: Goodbye
						=> DialogueEnd
					- Maybe a little bit.
						Farmer: Great use of my time. 
						Farmer: Good day. 
						=> DialogueEnd 
				- Fair enough, it is. 
					Farmer: Glad to hear you come to reason. Now leave me alone. 
					Farmer: I've got some preparations to make. 
					=> DialogueEnd 
				
- Alright. I'll leave you to it then. 
	Farmer: Thank you kindly. Pray the rest of the day will be just as peaceful. 
	=> DialogueEnd 

~ DialogueEnd
if (GameStateHolder.timeOfDay == 2):
	set GameStateHolder.SpokeToFarmerDayOneEvening = true
else:
	set GameStateHolder.SpokeToFarmerDayOneNoon = true	
do GameStateHolder.currentSpeaker.onConversationFinished()
=> END

#Play this dialogue if the player already talked to the farmer at noon and asked about the adventurers. 
~ SecondStart
Farmer: Back again? 
Farmer: What do you need this time?
- Here for the adventurers. You said they'd be around. 
	Farmer: So why are you talking to me? They're around the corner. 
	- Fair enough. See you around. 
	 => DialogueEnd
	- You don't seem to like them. 
		Farmer: I don't. 
		Farmer: But why do you care?
		- Because surely there must be a reason?
		- Because maybe there's a good story here!
		Farmer: Not there isn't! They came here and threatened me. 
		Farmer: Who in their right mind accuses a hard working citizen of attracting drakes to ruin the harvest
		Farmer: This is the stuff we eat! Why would I ruin it. 
		He shakes his head in annoyance and disbelief. 
		- A good point. 
		- Are you sure you didn't?
			Farmer: Oh not you too! 
			Farmer: Just leave me alone. 
			- No I was asking like on accident!
				Farmer: Oh...
				Farmer: I mean, maybe?
				Farmer: This is the first year we planted this new brown fruit. 
				Farmer: But it grows entirely underground! Why would a drake want it?
				- Good question. 
					Farmer: Doesn't make sense. And I'm not happy they're back to annoy me even more. 
					Farmer: Hey, you're a good liar, right?
					- I mean kinda?
						Farmer: Gots to be good enough. 
					- (Lie really well) Never told a lie in my life.
						He squints at you
						Farmer: First time for anything. 
					- Sure I am. 
					Farmer: Could you tell them something that would make them leave maybe?
					- I can try. 
						Farmer: It would be highly appreciated. 
						Farmer: On your way then!
						=> DialogueEnd
					- No, I'd rather not get myself in trouble for someone else. 
						Farmer: Well... doesn't harm to ask, right?
						Farmer: On your way then. 
						=> DialogueEnd 
				- Well, I'm out of guesses, good luck. 
					=> DialogueEnd 
			- Sure thing
				=> DialogueEnd 

#Play this dialogue if the Farmer is angry at the player. 
~ FarmerAngry
Farmer: I don't have anything else to tell you. 
Farmer: Good day. 
=> DialogueEnd 

#Play this dialogue if the farmer is not angry AND the player did not ask about the adventurers
~ Clueless
Farmer: Oh you again. 
Farmer: Good day?
- Yeah
	Farmer: Wish I could say the same
	=> DialogueEnd 
- Not really. 
	Farmer: Shame that. 
	Farmer: Hope it gets better!
	=> DialogueEnd 
	